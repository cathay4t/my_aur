# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: tobias [tobias [at] archlinux.org]
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Gris Ge <cnfourt@gmail.com>

pkgname=mutt-sidebar-gris
pkgver=1.5.23
pkgrel=1
pkgdesc='Small but very powerful text-based mail client'
url='http://www.mutt.org/'
license=('GPL')
backup=('etc/Muttrc')
arch=('i686' 'x86_64' 'armv7h' 'armv7l')
optdepends=('smtp-forwarder: to send mail')
depends=('gpgme' 'ncurses' 'openssl' 'libsasl' 'gdbm' 'libidn' 'mime-types' 'krb5')
conflicts=('mutt')
provides=('mutt')
source=("http://ftp.mutt.org/pub/mutt/mutt-${pkgver}.tar.gz"
        "patch-1.5.23.sidebar.20140412.txt"
        "mutt-1.5.21-pgp_verbose_mime.patch"
        )
sha512sums=('f1b4a7230253651857f61bd7215cce870a613012f613d4c907d401556083726c8ed7d429d57a8bf858c3b5b23683380d4c1494540d86ca80813e22cb6b95bc1e'
            'ac023f1ba5843a010de497b9cd85b22af6a30ceb8622c01be3c5b7e8e0761d8efe8e86e8c9b11aa96dcc405e8b1657672ec6da111b703d562bddf35ba848d34d'
            '40178b7ebc77458d8b331d228fef54f4355e8a32e51e50f885686688a019eea3b39803b2a254c00e5147d161e98840dcd2c9358580940b1699ece566bd0b32cc')

prepare() {
  cd "$srcdir/mutt-$pkgver"

  patch -Np1 -i "${srcdir}/patch-1.5.23.sidebar.20140412.txt"
  patch -Np1 -i "${srcdir}/mutt-1.5.21-pgp_verbose_mime.patch"

  # fix automake issue
  autoreconf -vfi
}

build() {
  cd "$srcdir/mutt-$pkgver"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-gpgme \
              --enable-pop \
              --enable-imap \
              --enable-smtp \
              --enable-hcache \
              --with-curses=/usr \
              --with-regex \
              --with-gss=/usr \
              --with-ssl=/usr \
              --with-sasl \
              --with-idn
  make
}

package() {
  cd "$srcdir/mutt-$pkgver"
  make DESTDIR="$pkgdir" install

  # remove unneeded or conflicting files
  rm "$pkgdir"/etc/mime.types{,.dist}
  rm "$pkgdir"/usr/bin/{flea,muttbug}
  rm "$pkgdir"/usr/share/man/man1/{flea,muttbug}.1

  # install Muttrc.gpg file
  install -D -m 644 contrib/gpg.rc "$pkgdir"/etc/Muttrc.gpg.dist
}
