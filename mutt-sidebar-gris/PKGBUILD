# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: tobias [tobias [at] archlinux.org]
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Gris Ge <cnfourt@gmail.com>

pkgname=mutt-sidebar-gris
pkgver=1.5.24
pkgrel=1
_sidebar_ver=20151111
pkgdesc='Small but very powerful text-based mail client'
url='http://www.mutt.org/'
license=('GPL')
backup=('etc/Muttrc')
arch=('i686' 'x86_64' 'armv7h' 'armv7l')
optdepends=('smtp-forwarder: to send mail')
depends=('gpgme' 'ncurses' 'openssl' 'libsasl' 'gdbm' 'libidn' 'mime-types' 'krb5')
conflicts=('mutt')
provides=('mutt')
source=("http://ftp.mutt.org/pub/mutt/mutt-${pkgver}.tar.gz"
        "http://lunar-linux.org/~tchan/mutt/patch-${pkgver}.sidebar.${_sidebar_ver}.txt"
        "mutt-1.5.21-pgp_verbose_mime.patch"
        )
sha256sums=('a292ca765ed7b19db4ac495938a3ef808a16193b7d623d65562bb8feb2b42200'
            '66441edf056032119f854fc5ee86c73eece8b95dc998c0cfae5ed05b0b035070'
            '6453030e2f0565734db0a3a42243c748e67fcc338dec7e937074e2f10193c5e3')

prepare() {
  cd "$srcdir/mutt-$pkgver"

  patch -Np1 -i "${srcdir}/patch-${pkgver}.sidebar.${_sidebar_ver}.txt"
  patch -Np1 -i "${srcdir}/mutt-1.5.21-pgp_verbose_mime.patch"

  # fix automake issue
  autoreconf -vfi
}

build() {
    cd "$srcdir/mutt-$pkgver"
    env SENDMAIL=%{_sbindir}/sendmail ISPELL=%{_bindir}/hunspell ./configure \
        --disable-external-dotlock \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-gpgme \
        --enable-pop \
        --enable-imap \
        --enable-smtp \
        --enable-hcache \
        --with-curses=/usr \
        --with-regex \
        --with-gss=/usr \
        --with-ssl=/usr \
        --with-sasl \
        --with-idn
  make
}

package() {
  cd "$srcdir/mutt-$pkgver"
  make DESTDIR="$pkgdir" install

  # remove unneeded or conflicting files
  rm "$pkgdir"/etc/mime.types{,.dist}
  rm "$pkgdir"/usr/bin/{flea,muttbug}
  rm "$pkgdir"/usr/share/man/man1/{flea,muttbug}.1

  # install Muttrc.gpg file
  install -D -m 644 contrib/gpg.rc "$pkgdir"/etc/Muttrc.gpg.dist
}
